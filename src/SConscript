#
# Copyright 2010-2014 Fabric Software Inc. All rights reserved.
#
import platform, json, os, shutil
Import('parentEnv', 'kl2edk', 'kl', 'extSuffix', 'STAGE_DIR')

extName = 'Xbox'

env = parentEnv.Clone()
env.Append(CPPPATH = [env.Dir('.').abspath])
env.Append(CPPPATH = [env.Dir('thirdparty').abspath])
env.Append(LIBS = ['libboost_thread-vc100-mt-s-1_55'])
env.Append(CCFLAGS = ['/EHsc'])

if platform.system().lower().startswith('win'):
  env.Append(LIBS = ['xinput'])
elif platform.system().lower().startswith('lin'):
  env.Append(LIBS = ['udev'])

# env.Append(LIBS = [
#   'ws2_32.lib',
#   'opengl32.lib',
#   'Dbghelp.lib',
#   'winmm.lib',
#   'kernel32.lib',
#   'user32.lib',
#   'gdi32.lib',
#   'winspool.lib',
#   'comdlg32.lib',
#   'advapi32.lib',
#   'shell32.lib',
#   'ole32.lib',
#   'oleaut32.lib',
#   'uuid.lib',
#   'odbc32.lib',
#   'odbccp32.lib'
# ])

sources = [
  env.File('XBox.fpm.json')
]
sources += env.Glob('*.kl')

cppFiles = [
  env.File('XBoxController_functions.cpp'),
  env.Dir('thirdparty').File('gamepad.c')
]

headers = [
  env.File('XBox.h')
]

extData = json.loads(open(sources[0].srcnode().abspath, 'r').read())
extVersion = extData['version']

extensionFiles = env.Install(STAGE_DIR, [env.File(extName+'.fpm.json')] + env.Glob('*.kl'))
kl2edkResults = env.RunKL2EDK(headers, sources)

extLibFileName = env.File(extName + '-' + extSuffix)
libraryFiles = Flatten([env.SharedLibrary(extLibFileName, cppFiles)])
env.Depends(libraryFiles, kl2edkResults)

extensionFiles += env.Install(STAGE_DIR, libraryFiles[0])
extensionFiles = Flatten(extensionFiles)

if os.environ.has_key('FABRIC_EXTS_INSTALL_DIR'):
  INSTALL_DIR = os.environ['FABRIC_EXTS_INSTALL_DIR']
  INSTALL_DIR = os.path.normpath(os.path.join(INSTALL_DIR, extName, extVersion))

  newExtensionFiles = []
  for sourceFile in extensionFiles:
    relPath = os.path.relpath(sourceFile.abspath, STAGE_DIR.abspath)
    targetPath = os.path.join(INSTALL_DIR, relPath)
    targetFile = env.File(targetPath)
    newExtensionFiles += env.Install(targetFile.dir, sourceFile)
  extensionFiles += Flatten(newExtensionFiles)

env.AlwaysBuild(extensionFiles)
alias = env.Alias(extName, extensionFiles)

Return('alias')
